#!/usr/bin/env zsh
# vim: foldmethod=indent

# This is a script to collect shell snippets until I develop them into real
# shell scripts.  "u" stands for "utils".  I got the idea from hans.

prog=${0##*/}

# Helper functions
function assert () {
  "$@"
  local ret=$?
  if [[ $ret -ne 0 ]]; then
    echo "Failed to execute '$@'." >&2
    exit 3
  fi
}
function start-bluetooth () {
  if [[ $(systemctl is-active bluetooth) != active ]]; then
    echo Starting bluetooth service ...
    sudo systemctl start bluetooth
  fi
}

# help and usage functions
function usage () {
  echo "$prog [options] subcommand [options] [arguments ...]"
  echo Possible subcommands are:
  #sed -n '/^case/,/^esac$/{s/^  \(.*\))\( *# *\(.*\)\)\{0,1\}$/\1 \3/p;}' $0
  sed -n '
    /^case/,/^esac$/{
      s/^  \([[:space:]]*(\{0,1\}[^()]*\))\( *# *\(.*\)\)\{0,1\}$/\1 \3/p
    }' $ZSH_SCRIPT \
  | while read arg help; do
      printf '%s\t%s\n' $arg $help
    done
}

# completion functions
# First define the completion function.  Avoid name colisions.
function _lucs_util_scipt_completion_function () {
  local state
  # First handle global options.  Everything that does not match a global
  # option as defined here is handled later.  The $state is set to
  # "subcommand" in this case.
  _arguments \
    '(- *)-h[show help]' \
    '(-h -x)-x[debug output]' \
    ':subcommand:->subcommand' \
    '*::options:->options' \
  && return 0
  case $state in
    subcommand)
      local -a subcommands_array
      subcommands_array=(
	'help:show help'
	'edit:open the script itself in $EDITOR'
	'bk:mount the external backup hd and make a backup'
	'phone:some phone related subcommands'
	'split-file-at-empty-lines:split a text files into several files'
	'backup:foo todo'
	'urxvt-font:set the font in this urxvt'
	'logs:show different logs'
	'setenv:set an environment var in a running process'
	'bank:open the bank browser'
	'python-profile:create a png from a python profileing run'
	'test-vim-startup:mesure and sort nvim startup time'
	'cmp-git-branch:run command on this branch and master (-u)'
	'true-color-test:test true color support of the terminal'
	'print:stage files for printing at the university (or --flush)'
	'time-management:control the time management warnings'
	'show-term-attributes:show all special font attributes of the terminal'
	'updateinfo:show info about upgradable packages with pacman'
	'nvim-nightly:'
      )
      # Use this array to complete the subcommands.
      _describe -t subcommands 'util subcommands' subcommands_array \
	&& ret=0
      ;;
    options)
      # TODO what is this?
      curcontext="${curcontext%:*}-${words[1]}:"
      case $words[1] in
	split-file-at-empty-lines) _arguments '*:: :_files';;
	urxvt-font) _arguments '*:: :_x_fonts';; # doesn't work yet
	print)
	  _arguments \
	    '--flush[flush the print queue now]:lp options:_u_helper_lp_options' \
	    '*:: :_files';;
	phone)
	  _arguments \
	    ':subcommand:->subcommand' \
	    '*:: :_files'
	  subcommands_array=(
	    put-vcf:'push contacts from a file to the phone'
	    savecontacts:'backup all contacts from phone to a file'
	  )
	  [[ $state = subcommand ]] \
	    && _describe -t subcommands 'phone subcommands' subcommands_array \
	    && ret=0
	  ;;
	logs)
	  _arguments ':subcommand:->subcommand' '*:: :_files'
	  subcommands_array=(
	    {fetchmail,mailin,pop}:'logs for fetchmail'
	    {smtp,mailout}:'logs for msmtp'
	    {backup,bk}:'logs for different backup scripts'
	  )
	  [[ $state = subcommand ]] \
	    && _describe -t subcommands 'logs subcommands' subcommands_array \
	    && ret=0
	  ;;
	time-management)
	  _arguments ':subcommand:->subcommand' '*:: :_files'
	  subcommands_array=(
	    list:'list the timers'
	    reset:'reset the timer'
	    start:'start a timer'
	    stop:'stop all timers'
	    pause:'pause all timers'
	    continue:'continue paused timers'
	  )
	  [[ $state = subcommand ]] \
	    && _describe -t subcommands 'time management subcommands' subcommands_array \
	    && ret=0
	  ;;
      esac
      ;;
    *) _message 'Error in completion function.';;
  esac
}
#sed -n '/^case/,/^esac$/{s/^  \(.*\))\( *# *\(.*\)\)\{0,1\}$/\1 \3/p;}' $0
# Last define some helper functions
function _u_helper_lp_options () {
  local service=lp
  _lp $@
}

while getopts hx FLAG; do
  case $FLAG in
    h) set -- help; break;;
    x) set -x;;
  esac
done

shift $((OPTIND - 1))


if [[ $# -eq 0 ]]; then
  set -- help
fi

case $1 in
  help) # show this help message
    usage
    ;;
  complete) # print setup code for completion (for eval-ing)
    # Print the defined completion function and the setup code.
    functions _lucs_util_scipt_completion_function
    print compdef _lucs_util_scipt_completion_function $prog
    functions _u_helper_lp_options
    ;;
  edit|e) # edit the source of this script
    # This is all in one line in order to force the shell to read it before
    # starting the editor.  Just in case some lines shift and such.
    $EDITOR $0; until zsh -n $0; do $EDITOR -q <(zsh -n $0); done; exit
    ;;
  bk) # mount the backup hard disk and make a backup
    until
      findmnt /media/bak >/dev/null
    do
      echo Please plug in the external backup disk.
      sleep 1
      mount LABEL=bak.fs
    done
    backup.py -v \
      snap \
      --exclude=/{.cache,.cargo,.cabal,.gem,.gradle,.rustup,.stack,.recoll,tmp,vid,audio,img,comic,TV-'*'.webl.h264.mp4,torrents,.local/share/{nvim/plugins,Zeal},.npm,vcs} \
      /media/bak/home
    ret=$?
    df -h /media/bak
    [[ $2 != -n ]] && umount /media/bak
    # We assume we are at home and now try to delete old backups from the pi
    #systemd-run --host pi
    ssh pi systemctl --user start clean-up-backup-snapshots
    exit $ret
    ;;
  bk-mini)
    set -e
    myip=$(curl --silent http://luc42.lima-city.de/ip.php)
    piip=$(dig +short luc.now-dns.net)
    if [[ $myip = $piip ]]
    then domain=pi
    else domain=nowip
    fi
    exec backup.py -v snapshot \
      --source etc --source mail --source .config --source uni \
      --exclude=/uni/'[^si]*' --exclude='*.m4v' \
      ${domain}:backup/tp/home
    ;;
  phone) # phone related subcommands (nokia)
    shift
    case $1 in
      savecontacts)
	# check bluetooth service
	start-bluetooth
	echo Connecting to Nokia phone ...
	bluetoothctl <<-EOF
	power on
	connect 00:25:47:10:0D:2D
	exit
	EOF
	# Wait for the connection.
	sleep 1
	gammu backup $2.vcf <<<ALL
	;;
      put-vcf)
	start-bluetooth
	echo Connecting to Nokia phone ...
	bluetoothctl <<-EOF
	power on
	connect 00:25:47:10:0D:2D
	exit
	EOF
	# Wait for the connection.
	sleep 1
	tmp1=$(mktemp --suffix=.vcf)
	tmp2=$(mktemp --suffix=.vcf)
	sed '/^ADR[;:]/d' < $2 > $tmp1
	iconv -c --to-code=latin1 --output=$tmp2 $tmp1
	gammu restore $tmp2 <<<ALL
	rm -f $tmp1 $tmp2
	;;
      *)
	echo TODO >&2
	exit 2
	;;
    esac
    ;;
  split-file-at-empty-lines)
    shift
    empty=''
    extension=.vcf
    while [[ $# -ge 1 ]]; do
      case $1 in
	--windows|--crlf|--dos)
	  empty=$'\r'
	  shift
	  ;;
	-d|--dir|--out|-o|--output)
	  if [[ $# -eq 1 ]]; then
	    echo The option $1 needs an argument. >&2
	    exit 2
	  fi
	  output=$2
	  shift 2
	  ;;
	--extension)
	  if [[ $# -eq 1 ]]; then
	    echo The option $1 needs an argument. >&2
	    exit 2
	  fi
	  extension=$2
	  shift 2
	  ;;
	--)
	  shift
	  break
	  ;;
	*)
	  break
	  ;;
      esac
    done
    if [[ $# -ne 1 ]]; then
      echo You need to give exactly one filename. >&2
      exit 2
    fi
    file=$1
    if [[ -z $output ]]; then
      output=$file.split
    fi
    # find the number of empty lines (corresponds to the number of files
    # created) and then count the bytes in the decimal representation, which
    # gives the length to which output filename numbers should be padded with
    # zeros
    length=$(($(echo $(($(grep --count "^$empty\$" $file) + 1)) | wc --bytes) - 1))
    integer -Z $length i=0
    mkdir -p $output
    rm -f $output/*(N)
    while read line; do
      if [[ $line = $empty ]]; then
	((i++))
	rm -f $output/$i$extension
      else
	echo $line >> $output/$i$extension
      fi
    done < $file
    ;;
  backup)
    backup.py snapshot --source ~/.config/gpg/ --exclude \
      $(repeat 14 printf '[0-9]') --exclude latest \
       nowip:backup/gpg
    backup.py snapshot --source $PASSWORD_STORE_DIR/ --exclude .git \
      --exclude .gitattributes \
      nowip:backup/pass
    ;;
  urxvt-font)
    printf '\33]50;%s\007' ${2:-xft:terminus};;
  logs)
    shift
    case $1 in
      bk|backup)
	systemctl --no-pager --full status \
	  backup-pacman-cache.timer \
	  backup.py@-home-luc-.config-backup.py-arch.timer
	systemctl --no-pager --full --user status \
	  backup.py@-home-luc-.config-backup.py-home.timer
	echo "==============================================================="
	systemctl --no-pager --full status \
	  backup-pacman-cache.service \
	  backup.py@-home-luc-.config-backup.py-arch.service
	systemctl --no-pager --full --user status \
	  backup.py@-home-luc-.config-backup.py-home.service
	;;
      pop|mailin|fetchmail)
	journalctl --quiet --no-pager --lines=$((2*LINES)) \
	  --user-unit=fetchmail.service
	;;
      smtp|mailout)
	journalctl --quiet --no-pager --lines=$((2*LINES)) \
	  SYSLOG_IDENTIFIER=msmtp
	;;
      mail|mailall|allmail)
	shift
	journalctl --quiet --no-pager --lines=$((2*LINES)) "$@" \
	  SYSLOG_IDENTIFIER=msmtp + _SYSTEMD_USER_UNIT=fetchmail.service
	;;
      *) print Choose one of bk, pop or smtp. >&2; exit 2;;
    esac
    ;;
  setenv) # <PID> VARNAME=value
    shift
    # thanks to https://gist.github.com/coderofsalvation/e1376e4d2b29607431df
    sudo gdb -q -batch -ex "attach $1" -ex "call putenv(\"$2\")" -ex 'detach'
    ;;
  bank)
    shift
    browser -p \
      https://www.onlinebanking-vr-ll.de/banking-private/entry \
      https://banking.umweltbank.de/ $@ &
    ;;
  python-profile)
    shift
    name=profile
    while getopts n: FLAG; do
      case $FLAG in
	n) name=$OPTARG;;
      esac
    done
    shift $((OPTIND - 1))
    PYTHONPATH=. python -m cProfile -o $name.pstats $@
    gprof2dot -f pstats $name.pstats > $name.dot
    dot -T png -o $name.png $name.dot
    feh $name.png
    ;;
  test-vim-startup)
    shift
    searchterm=${1:-sourcing}
    echo " clock   self+sourced   self:  sourced script"
    nvim --headless -c 'quitall!' --startuptime \
      >(grep ${@:-sourcing} | sort -n --key=3) \
      2>/dev/null
    echo " clock   self+sourced   self:  sourced script"
    ;;
  cmp-git-branch)
    shift
    upsteam=master
    count=10
    while getopts u:c: FLAG; do
      case $FLAG in
	u) upstream=$OPTARG;;
	c) count=$OPTARG;;
      esac
    done
    shift $((OPTIND - 1))
    # Save the current HEAD.
    head=$(git rev-parse --abbrev-ref HEAD)
    time (for i in {0..$count}; do $@ &>/dev/null; done )
    git checkout $upstream
    time (for i in {0..$count}; do $@ &>/dev/null; done )
    git checkout $head
    ;;
  true-color-test) # print some colors to test true color support of the term
    awk 'BEGIN{
    s="/\\/\\/\\/\\/\\"; s=s s s s s s s s;
    for (colnum = 0; colnum<77; colnum++) {
      r = 255-(colnum*255/76);
      g = (colnum*510/76);
      b = (colnum*255/76);
      if (g>255) g = 510-g;
	printf "\033[48;2;%d;%d;%dm", r,g,b;
	printf "\033[38;2;%d;%d;%dm", 255-r,255-g,255-b;
	printf "%s\033[0m", substr(s,colnum+1,1);
      }
      printf "\n";
    }'
    ;;
  print)
    shift
    if [[ $1 = --flush ]]; then
      shift
      ssh ifi bin/flush-print-queue.sh $@
    else
      scp -p $@ ifi:print/
    fi
    ;;
  time-management)
    shift
    function list-loaded-timers () {
      systemctl --user list-units --plain 'time-management-warning@*.timer' |\
	sed -n '1d;/^$/q;s/ .*//p'
    }
    case $1 in
      list) systemctl --user list-timers 'time-management-warning@*.timer';;
      reset) systemctl --user start time-management-warning;;
      start)
	if [[ -z $2 ]]; then
	  echo You have to give a time argument. >&2
	  exit 2
	fi
	systemctl --user start time-management-warning@$2.timer
	;;
      stop)
	for timer in $(list-loaded-timers); do
	  systemctl --user stop $timer
	done
	;;
      pause) systemctl --user mask --runtime timer-management-warning;;
      continue) systemctl --user unmask --runtime timer-management-warning;;
      *) echo argument error >&2; exit 2;;
    esac
    ;;
  show-term-attributes)
    for i in {0..15}; do
      printf 'i=%-3s --[\e[%sm terminal code test string \e[0m]--\n' $i: $i
    done
    ;;
  updateinfo)
    pacman -Qquet | xargs --no-run-if-empty pacman -Qi | \
      sed -E '/(^(Version|Architecture|Licenses|Packager|Build Date|Install Date|Install Reason|Install Script|Validated By)|None$)/d; s/^Name.*/\x1b[31m&\x1b[m/'
    ;;
  nvim-nightly)
    wget --output-document ~/.local/bin/nvim-nightly https://github.com/neovim/neovim/releases/download/nightly/nvim.appimage
    chmod +x ~/.local/bin/nvim-nightly
    ;;
  *) echo Unknown command >&2; exit 2;;
esac
